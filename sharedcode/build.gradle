apply plugin: 'kotlin-multiplatform'
apply plugin: 'kotlinx-serialization'

def ktor_version = '1.1.2'

kotlin {
    targets {
        final def iOSTarget = System.getenv('SDK_NAME')?.startsWith("iphoneos") ? presets.iosArm64 : presets.iosX64

        fromPreset(iOSTarget, 'iOS') {
            compilations.main.outputKinds('FRAMEWORK')
        }

        fromPreset(presets.jvm, 'android')
    }

    sourceSets {
        commonMain.dependencies {
            api 'org.jetbrains.kotlin:kotlin-stdlib-common'
            implementation 'org.jetbrains.kotlinx:kotlinx-serialization-runtime-common:0.10.0'
            implementation "com.soywiz:klock:1.1.1"
            implementation "io.ktor:ktor-client-core:$ktor_version"
            implementation "io.ktor:ktor-client-logging:$ktor_version"
            implementation "io.ktor:ktor-client-json:$ktor_version"
        }

        androidMain {

            dependencies {
                api 'org.jetbrains.kotlin:kotlin-stdlib'
                implementation 'net.danlew:android.joda:2.10.1'
                implementation 'org.greenrobot:eventbus:3.1.1'
                implementation "org.jetbrains.kotlinx:kotlinx-serialization-runtime:0.10.0"
                implementation "io.ktor:ktor-client-android:$ktor_version"
                implementation "io.ktor:ktor-client-logging-jvm:$ktor_version"
            }
        }

        iOSMain {
            dependencies {
//                implementation "org.jetbrains.kotlinx:kotlinx-coroutines-core-native:1.0.1"
                implementation "org.jetbrains.kotlinx:kotlinx-serialization-runtime-native:0.10.0"
                implementation "io.ktor:ktor-client-ios:$ktor_version"
                implementation "io.ktor:ktor-client-logging-iosx64:$ktor_version"
                implementation "io.ktor:ktor-client-json-iosx64:$ktor_version"
            }
        }
        
    }
}

// workaround for https://youtrack.jetbrains.com/issue/KT-27170
configurations {
    compileClasspath
}

task packForXCode(type: Sync) {
    final File frameworkDir = new File(buildDir, "xcode-frameworks")
    final String mode = project.findProperty("XCODE_CONFIGURATION")?.toUpperCase() ?: 'DEBUG'

    inputs.property "mode", mode
    dependsOn kotlin.targets.iOS.compilations.main.linkTaskName("FRAMEWORK", mode)

    from { kotlin.targets.iOS.compilations.main.getBinary("FRAMEWORK", mode).parentFile }
    into frameworkDir

    doLast {
        new File(frameworkDir, 'gradlew').with {
            text = "#!/bin/bash\nexport 'JAVA_HOME=${System.getProperty("java.home")}'\ncd '${rootProject.rootDir}'\n./gradlew \$@\n"
            setExecutable(true)
        }
    }
}

tasks.build.dependsOn packForXCode